// Package and import specifications section.

package compiler488.parser;

import java_cup.runtime.*;		// Must always import this.
import compiler488.ast.*;
import compiler488.ast.decl.*;
import compiler488.ast.expn.*;
import compiler488.ast.stmt.*;
import compiler488.ast.type.*;


/* User code components 1: action code components.
 * Code to be used inside the actions triggered when productions are matched.
 * If any common code is needed for more than one action , put it here.
 */
// action code {:
// your code goes HERE
//:};

/* User code components 2: parser code components.
 * Modifications to the default generated parser
 */
parser code {:

	String lastError;

    /** Override the report_error method so it will display the line and
     * column of where the error occurred in the input as well as the
     * reason for the error which is passed into the method in the
     * String 'message'.
     * @param message  error message to print
     * @param info     symbol containing line/column numbers
     */
    public void report_error(String message, Object info)
	{
	String st =  "Error";

        if (info instanceof java_cup.runtime.Symbol)
	    {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

	    /* s.left is supposed to hold the line number of the error.
	     * s.right is supposed to hold the column number.
             * If either is < 0 the parser may have run off the end of the program
             * and a Syntax Error message without line number may be printed.
	     */

            if (s.left >= 0)				// Check line number.
		{
                st += " in line " + (s.left + 1);
                if (s.right >= 0)			// Check column number.
                    st += ", column " + (s.right + 1);
		}
            else
               st += " at end of input " ;
           }
	    st += ": " + message;
        System.err.println (st);
        lastError = st;
	}

    /** Override the report_fatal_error method to use the report_error method. */
    /** @throws SyntaxErrorException						*/
    public void report_fatal_error (String message, Object info) throws SyntaxErrorException
	{
        report_error (message, info);
        throw new SyntaxErrorException (lastError);
	}

:};

/* User code components 3: initialization code.
 * If you want some code to be executed before the parser asks for the first
 * token, add it here.
 */
init with {:
:};

/* User code components 4: scanner specification code.
 * If you need to override the way the parser asks the scanner for the
 * next token, put your preferred method here.  Uncomment the line following
 * this comment, replace the line after that with your code, and uncomment
 * the line after that.
 */
// scan with {:
// your code goes here
// :};

/* Symbol lists section.
 * The names of terminals and non-terminals are not allowed to be CUP
 * reserved words.  These include "code", "action", "parser", "terminal",
 * "non", "nonterminal", "init", "scan", "with", "start", "precedence",
 * "left", "right", "nonassoc", "import", and "package".
 */

// DEFINITIONS for CSC488S Source Language  Winter  2009/2010

// Terminals returned by the scanner with no value attached.
terminal	AND,	OR,	NOT,	TRUE,	FALSE	;
terminal 	BOOLEAN,INTEGER,FUNCTION,PROCEDURE	;
terminal	BEGIN,	DO,	ELSE,	END,	EXIT	;
terminal	IF,	RETURN,	LOOP,	PUT,	GET	;
terminal	THEN,	WHILE,	SKIP,	YIELDS, WHEN	;

// Special-character terminals, no value attached
terminal	L_PAREN	,R_PAREN,L_SQUARE, R_SQUARE, L_CURLEY, R_CURLEY	;
terminal	EQUAL,	GREATER, LESS,	COMMA,  DOT	;
terminal	PLUS,	MINUS,	TIMES,  DIVIDE	;

// Terminals returned by the scanner with a value.
terminal String  IDENT	;
terminal Integer INTCONST	;
terminal String  TEXTCONST	;

// Non-terminals that have no value.
// nonterminal xxx

nonterminal Program program;
nonterminal Scope scope;

nonterminal ASTList<Stmt> declarationsAndStatements;
nonterminal Stmt statement;
nonterminal Declaration declaration;
nonterminal ProcedureCallStmt procedureCall;

nonterminal Expn expression;
nonterminal Expn conjunction;
nonterminal Expn negation;
nonterminal Expn compareExpn;
nonterminal Expn arithExpn;
nonterminal Expn term;
nonterminal Expn factor;
nonterminal Expn primary;
nonterminal ConstExpn constant;
nonterminal Expn variable;
nonterminal ASTList<Expn> arguments;

nonterminal ASTList<DeclarationPart> variablenames;
nonterminal DeclarationPart variablename;
nonterminal Integer[] bound;
nonterminal Integer generalBound;
nonterminal Type type;
nonterminal ASTList<ScalarDecl> parameters;
nonterminal ScalarDecl parameter;
nonterminal ASTList<Expn> inputs;
nonterminal ASTList<Printable> outputs;
nonterminal Printable output;

// Non-terminals that have a value.
// nonterminal type  nameList ;

// PLEASE NOTE: your grammar can not use the 'precedence'
// feature of java-cup. The lines below should remain
// commented.
//
//precedence left PLUS, MINUS;
//precedence left TIMES, DIVIDE;		// Higher precedence.

// The grammar section.

// This specifies the initial symbol of the grammar.
start with program;

/* Notice that sometimes the action must be inside the production rule,
 * not after it.  This is a subtle yet significant difference from YACC.
 * This is because the CUP parser is looking ahead one token.  It ALWAYS
 * reads one more token before it will act on the production.
 */

program ::= scope:s
			{: RESULT = new Program(s, new SourceCoord(sleft, sright)); :}
	;

scope ::= BEGIN:b declarationsAndStatements:d END
			{: RESULT = new Scope(d, new SourceCoord(bleft, bright)); :}
	|	BEGIN:b END
			{: RESULT = new Scope(new SourceCoord(bleft, bright)); :}
	;

//  sequence of arbitrarily intermixed declarations and statements
declarationsAndStatements ::= statement:s
			{: RESULT = new ASTList(s); :}
	|	declaration:d
			{: RESULT = new ASTList(d); :}
	|	declarationsAndStatements:l statement:s
			{: RESULT = l.append(s); :}
	|	declarationsAndStatements:l declaration:d
			{: RESULT = l.append(d); :}
	;

statement ::= variable:v LESS EQUAL expression:e
			{: RESULT = new AssignStmt(v, e, new SourceCoord(vleft, vright)); :}
	|	IF:i expression:e THEN declarationsAndStatements:d END
			{: RESULT = new IfStmt(e, d, new SourceCoord(ileft, iright)); :}
	|	IF:i expression:e THEN declarationsAndStatements:d1 ELSE declarationsAndStatements:d2 END
			{: RESULT = new IfStmt(e, d1, d2, new SourceCoord(ileft, iright)); :}
	|	WHILE:w expression:e DO declarationsAndStatements:d END
			{: RESULT = new WhileDoStmt(e, d, new SourceCoord(wleft, wright)); :}
	|	LOOP:l declarationsAndStatements:d END
			{: RESULT = new LoopStmt(d, new SourceCoord(lleft, lright)); :}
	|	EXIT:e
			{: RESULT = new ExitStmt(new SourceCoord(eleft, eright)); :}
	|	EXIT:b WHEN expression:e
			{: RESULT = new ExitStmt(e, new SourceCoord(bleft, bright)); :}
	|	RETURN:r
			{: RESULT = new ReturnStmt(new SourceCoord(rleft, rright)); :}
	|	RETURN:r L_PAREN expression:e R_PAREN
			{: RESULT = new ReturnStmt(e, new SourceCoord(rleft, rright)); :}
	|	PUT:p outputs:o
			{: RESULT = new PutStmt(o, new SourceCoord(pleft, pright)); :}
	|	GET:g inputs:i
			{: RESULT = new GetStmt(i, new SourceCoord(gleft, gright)); :}
	|	procedureCall:c
			{: RESULT = c; :}
	|	scope:s
			{: RESULT = s; :}
	;

procedureCall ::= IDENT:i
			{: RESULT = new ProcedureCallStmt(i, new SourceCoord(ileft, iright)); :}
	|	IDENT:i L_PAREN arguments:a R_PAREN
			{: RESULT = new ProcedureCallStmt(i, a, new SourceCoord(ileft, iright)); :}
	;

declaration ::= type:t variablenames:v
			{: RESULT = new MultiDeclarations(t, v, new SourceCoord(tleft, tright)); :}
	|	type:t FUNCTION IDENT:i scope:s
			{: RESULT = new RoutineDecl(i, t, s, new SourceCoord(tleft, tright)); :}
	|	type:t FUNCTION IDENT:i L_PAREN parameters:p R_PAREN scope:s
			{: RESULT = new RoutineDecl(i, t, p, s, new SourceCoord(tleft, tright)); :}
	|	PROCEDURE:p IDENT:i scope:s
			{: RESULT = new RoutineDecl(i, s, new SourceCoord(pleft, pright)); :}
	|	PROCEDURE:b IDENT:i L_PAREN parameters:p R_PAREN scope:s
			{: RESULT = new RoutineDecl(i, p, s, new SourceCoord(bleft, bright)); :}
	;

variablenames ::= variablename:v
			{: RESULT = new ASTList(v); :}
	|	variablenames:v1 COMMA variablename:v2
			{: RESULT = v1.append(v2); :}
	;

variablename ::= IDENT:i
			{: RESULT = new ScalarDeclPart(i, new SourceCoord(ileft, iright)); :}
	|	IDENT:i L_SQUARE bound:b R_SQUARE
			{: RESULT = new ArrayDeclPart(i, b, new SourceCoord(ileft, iright)); :}
	|	IDENT:i L_SQUARE bound:b1 COMMA bound:b2  R_SQUARE
			{: RESULT = new ArrayDeclPart(i, b1, b2, new SourceCoord(ileft, iright)); :}
	;

bound ::= INTCONST:i
			{: RESULT = new Integer[] { 1, i }; :}
	|	generalBound:b1 DOT DOT generalBound:b2
			{: RESULT = new Integer[] { b1, b2 }; :}
	;

generalBound ::= INTCONST:i
			{: RESULT = i; :}
	|	MINUS INTCONST:i
			{: RESULT = -i; :}
	;

type ::= INTEGER:i
			{: RESULT = new IntegerType(new SourceCoord(ileft, iright)); :}
	|	BOOLEAN:b
			{: RESULT = new BooleanType(new SourceCoord(bleft, bright)); :}
	;

outputs	::= output:o
			{: RESULT = new ASTList(o); :}
	|	outputs:o1 COMMA output:o2
			{: RESULT = o1.append(o2); :}
	;

output ::= expression:e
			{: RESULT = e; :}
	|	TEXTCONST:t
			{: RESULT = new TextConstExpn(t, new SourceCoord(tleft, tright)); :}
	|	SKIP:s
			{: RESULT = new SkipConstExpn(new SourceCoord(sleft, sright)); :}
	;

inputs ::= variable:v
			{: RESULT = new ASTList(v); :}
	|	inputs:i COMMA variable:v
			{: RESULT = i.append(v); :}
	;

arguments ::= expression:e
			{: RESULT = new ASTList(e); :}
	|	arguments:a COMMA expression:e
			{: RESULT = a.append(e); :}
	;

parameters ::= parameter:p
			{: RESULT = new ASTList(p); :}
	|	parameters:p1 COMMA parameter:p2
			{: RESULT = p1.append(p2); :}
	;

parameter ::= type:t IDENT:i
			{: RESULT = new ScalarDecl(i, t, new SourceCoord(tleft, tright)); :}
	;

expression ::= conjunction:c
			{: RESULT = c; :}
	|	expression:e OR conjunction:c
			{: RESULT = new BoolExpn(BoolExpn.OP_OR, e, c, new SourceCoord(eleft, eright)); :}
	;

conjunction	::=	negation:n
			{: RESULT = n; :}
	|	conjunction:c AND negation:n
			{: RESULT = new BoolExpn(BoolExpn.OP_AND, c, n, new SourceCoord(cleft, cright)); :}
	;

negation ::= compareExpn:c
			{: RESULT = c; :}
	|	NOT negation:n
			{: RESULT = new NotExpn(n, new SourceCoord(nleft, nright)); :}
	;

compareExpn ::= arithExpn:a
			{: RESULT = a; :}
	|	arithExpn:a1 EQUAL arithExpn:a2
			{: RESULT = new EqualsExpn(EqualsExpn.OP_EQUAL, a1, a2, new SourceCoord(a1left, a1right)); :}
	|	arithExpn:a1 NOT EQUAL arithExpn:a2
			{: RESULT = new EqualsExpn(EqualsExpn.OP_NOT_EQUAL, a1, a2, new SourceCoord(a1left, a1right)); :}
	|	arithExpn:a1 LESS arithExpn:a2
			{: RESULT = new CompareExpn(CompareExpn.OP_LESS, a1, a2, new SourceCoord(a1left, a1right)); :}
	|	arithExpn:a1 LESS EQUAL arithExpn:a2
			{: RESULT = new CompareExpn(CompareExpn.OP_LESS_EQUAL, a1, a2, new SourceCoord(a1left, a1right)); :}
	|	arithExpn:a1 GREATER arithExpn:a2
			{: RESULT = new CompareExpn(CompareExpn.OP_GREATER, a1, a2, new SourceCoord(a1left, a1right)); :}
	|	arithExpn:a1 GREATER EQUAL arithExpn:a2
			{: RESULT = new CompareExpn(CompareExpn.OP_GREATER_EQUAL, a1, a2, new SourceCoord(a1left, a1right)); :}
	;

arithExpn ::= term:t
			{: RESULT = t; :}
	|	arithExpn:a PLUS term:t
			{: RESULT = new ArithExpn(ArithExpn.OP_PLUS, a, t, new SourceCoord(aleft, aright)); :}
	|	arithExpn:a MINUS term:t
			{: RESULT = new ArithExpn(ArithExpn.OP_MINUS, a, t, new SourceCoord(aleft, aright)); :}
	;

term ::= factor:f
			{: RESULT = f; :}
	|	term:t TIMES factor:f
			{: RESULT = new ArithExpn(ArithExpn.OP_TIMES, t, f, new SourceCoord(tleft, tright)); :}
	|	term:t DIVIDE factor:f
			{: RESULT = new ArithExpn(ArithExpn.OP_DIVIDE, t, f, new SourceCoord(tleft, tright)); :}
	;

factor ::= primary:p
			{: RESULT = p; :}
	|	MINUS factor:f
			{: RESULT = new UnaryMinusExpn(f, new SourceCoord(fleft, fright)); :}
	;

primary ::= constant:c
			{: RESULT = c; :}
	|	variable:v
			{: RESULT = v; :}
	|	IDENT:i L_PAREN arguments:a R_PAREN
			{: RESULT = new FunctionCallExpn(i, a, new SourceCoord(ileft, iright)); :}
	| 	L_PAREN  expression:e  R_PAREN
			{: RESULT = e; :}
	|	L_CURLEY:l declarationsAndStatements:d YIELDS expression:e R_CURLEY
			{: RESULT = new AnonFuncExpn(d, e, new SourceCoord(lleft, lright)); :}
	;

constant ::= INTCONST:i
			{: RESULT = new IntConstExpn(i, new SourceCoord(ileft, iright)); :}
	|	TRUE:t
			{: RESULT = new BoolConstExpn(true, new SourceCoord(tleft, tright)); :}
	|	FALSE:f
			{: RESULT = new BoolConstExpn(false, new SourceCoord(fleft, fright)); :}
	;

variable ::= IDENT:i
			{: RESULT = new IdentExpn(i, new SourceCoord(ileft, iright)); :}
	|	IDENT:i L_SQUARE expression:e R_SQUARE
			{: RESULT = new SubsExpn(i, e, new SourceCoord(ileft, iright)); :}
	|	IDENT:i L_SQUARE expression:e1 COMMA expression:e2 R_SQUARE
			{: RESULT = new SubsExpn(i, e1, e2, new SourceCoord(ileft, iright)); :}
	;
